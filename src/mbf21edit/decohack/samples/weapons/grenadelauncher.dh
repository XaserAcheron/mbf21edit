/*****************************************************************************
 * mbf21edit - decohack - root
 * (c) xaser acheron 2024
 *     MIT License
 *****************************************************************************
 * Grenade Launcher
 * 
 * Fires a non-bouncy low-gravity grenade with a wide explosion radius.
 * 
 * Demonstrates:
 *  A_WeaponProjectile
 *  A_ConsumeAmmo
 *  A_RadiusDamage
 *  A_SpawnObject
 *  A_AddFlags
 *  A_RemoveFlags
 *  LOGRAV flag
 *  FORCERADIUSDMG flag
 * 
 ****************************************************************************/

weapon SampleGrenadeLauncher "Grenade Launcher"
{
	states
	{
		Fire:
			MISG B 8 A_GunFlash
			MISG B 0 A_ConsumeAmmo
			MISG B 12 A_WeaponProjectile(SampleGrenade, 0.0, -5.0)
			MISG B 0 A_ReFire
			Goto Ready
	}
}

thing SampleGrenade "Grenade"
{
	Damage 20
	Speed 15
	Radius 8
	Height 8

	SeeSound rlaunc
	DeathSound barexp

	+MISSILE
	+DROPOFF
	+NOBLOCKMAP
	+LOGRAV
	+FORCERADIUSDMG

	states
	{
		Spawn:
			MISL A 5
			loop
		Death:
			MISL B 0 Bright A_AddFlags(NOGRAVITY)
			MISL B 0 Bright A_RemoveFlags(0, LOGRAV)
			MISL B 4 Bright A_RadiusDamage(128, 256)
			MISL B 4 Bright A_SpawnObject(SampleGrenadeBoom,-45.0, -48.0, 0.0,  32.0)
			MISL C 2 Bright A_SpawnObject(SampleGrenadeBoom, 45.0, -48.0, 0.0,   0.0)
			MISL C 4 Bright A_SpawnObject(SampleGrenadeBoom,  0.0, -48.0, 0.0, -32.0)
			MISL D 4 Bright
			stop
	}
}

thing SampleGrenadeBoom "Grenade Sub-Explosion"
{
	+NOGRAVITY
	+TRANSLUCENT

	states
	{
		Spawn:
			MISL B 1 Bright
			MISL B 8 Bright A_PlaySound(barexp)
			MISL C 6 Bright
			MISL D 4 Bright
			stop
	}
}
