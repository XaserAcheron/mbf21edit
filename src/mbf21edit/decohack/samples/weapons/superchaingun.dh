/*****************************************************************************
 * mbf21edit - decohack - root
 * (c) xaser acheron 2024
 *     MIT License
 *****************************************************************************
 * Super Chaingun
 * 
 * A super-chaingun with a wind-up, firing automatic mini-shotgun blasts.
 * 
 * Demonstrates:
 *  "SILENT" flag
 *  "ammopershot" field
 *  A_WeaponBulletAttack
 *  A_WeaponSound
 *  A_WeaponAlert
 *  A_ConsumeAmmo
 *  A_GunFlashTo
 *  A_RefireTo
 * 
 ****************************************************************************/

weapon SampleSuperChaingun "Super Chaingun"
{
	+SILENT
	ammopershot 3
	states
	{
		Flash1:
			CHGF A 2 A_Light1
			goto lightdone
		Flash2:
			CHGF B 2 A_Light2
			goto lightdone
		Fire:
			CHGG A 4
			CHGG BA 3
			CHGG BAB 2
		FireLoop:
			CHGG A 0 A_WeaponAlert
			CHGG A 0 A_ConsumeAmmo(2)
			CHGG A 0 A_GunFlashTo(Flash1, 0)
			CHGG A 0 A_WeaponSound(shotgn)
			CHGG A 2 A_WeaponBulletAttack(5.6, 0.0, 2)
			CHGG A 0 A_ConsumeAmmo(1)
			CHGG A 0 A_GunFlashTo(Flash2, 1)
			CHGG B 2 A_WeaponBulletAttack(11.2, 7.1, 1)
			CHGG A 2 A_ReFireTo(FireLoop, 0)
		FireEnd:
			CHGG BAB 2
			CHGG AB 3
			CHGG A 4
			CHGG B 5
			Goto Ready
	}
}
