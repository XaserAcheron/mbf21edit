/*****************************************************************************
 * mbf21edit - decohack
 * (c) mtrop 2024
 *     MIT License
 *****************************************************************************
 * 
 * Feature: A_JumpIfTargetInSight
 * 
 * Defines an imp that runs faster if it sees its target.
 *
 ****************************************************************************/

// Declarations

auto thing A_JumpIfTargetInSight__Imp {}

// Definitions

thing A_JumpIfTargetInSight__Imp : thing MTF_IMP "A_JumpIfTargetInSight - Imp"
{
	//$Category MBF21Edit
	EdNum 3115

	States
	{
		See:
			TROO AABBCCDD 3 {
				A_Chase()
				A_JumpIfTargetInSight(Pursue, 180.0)
			}
			Loop
		Pursue:
			TROO AABBCCDD 2 A_Chase()
			TROO A 0 A_JumpIfTargetInSight(Pursue, 180.0)
			Goto See // ensure it hits the new "see"
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 6 A_TroopAttack
			Goto See // ensure it hits the new "see"
		Pain:
			TROO H 2
			TROO H 2 A_Pain
			Goto See // ensure it hits the new "see"
		Raise:
			TROO ML 8
			TROO KJI 6
			Goto See // ensure it hits the new "see"
	}
	
}
