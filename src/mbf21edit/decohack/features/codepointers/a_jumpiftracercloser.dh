/*****************************************************************************
 * mbf21edit - decohack
 * (c) mtrop 2024
 *     MIT License
 *****************************************************************************
 * 
 * Feature: A_JumpIfTracerCloser
 * 
 * Defines an imp that fires a tracer fireball that detonates itself when it 
 * gets close to its target.
 *
 ****************************************************************************/

// Declarations

auto thing A_JumpIfTracerCloser__Fireball {}
auto thing A_JumpIfTracerCloser__Imp {}

// Definitions

thing A_JumpIfTracerCloser__Fireball : thing MTF_IMP_FIREBALL "A_JumpIfTracerCloser - Fireball"
{
	DeathSound "BAREXP"

	States
	{
		Spawn:
			BAL1 AB 4 BRIGHT A_SeekTracer(5.0, 30.0)
			BAL1 A 0 BRIGHT A_JumpIfTracerCloser(Death, 96.0)
			Loop
		Death:
			MISL B 8 BRIGHT {
				A_Stop()
				A_Explode()
				A_Scream() // necessary if detonating in mid-air
			}
			MISL C 6 BRIGHT
			MISL D 4 BRIGHT
			Stop
	}
	
}

thing A_JumpIfTracerCloser__Imp : thing MTF_IMP "A_JumpIfTracerCloser - Imp"
{
	//$Category MBF21Edit
	EdNum 3118

	States
	{
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 6 A_MonsterProjectile(A_JumpIfTracerCloser__Fireball)
			Goto See
	}
	
}
