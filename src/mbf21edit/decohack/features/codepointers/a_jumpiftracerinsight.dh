/*****************************************************************************
 * mbf21edit - decohack
 * (c) mtrop 2024
 *     MIT License
 *****************************************************************************
 * 
 * Feature: A_JumpIfTracerInSight
 * 
 * Defines an imp that fires a tracer fireball that traces more aggressively
 * if it "sees" its target.
 *
 ****************************************************************************/

// Declarations

auto thing A_JumpIfTracerInSight__Fireball {}
auto thing A_JumpIfTracerInSight__Imp {}

// Definitions

thing A_JumpIfTracerInSight__Fireball : thing MTF_IMP_FIREBALL "A_JumpIfTracerInSight - Fireball"
{
	States
	{
		Spawn:
			BAL1 AB 4 BRIGHT {
				A_SeekTracer(10.0, 10.0)
				A_JumpIfTracerInSight(BetterTrace, 90.0)
			}
			Loop
		BetterTrace:
			APLS AB 4 BRIGHT A_SeekTracer(5.0, 45.0)
			APLS A 0 BRIGHT A_JumpIfTracerInSight(BetterTrace, 90.0)
			Goto Spawn
	}
	
}

thing A_JumpIfTracerInSight__Imp : thing MTF_IMP "A_JumpIfTracerInSight - Imp"
{
	//$Category MBF21Edit
	EdNum 3117

	States
	{
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 6 A_MonsterProjectile(A_JumpIfTracerInSight__Fireball)
			Goto See
	}
	
}
