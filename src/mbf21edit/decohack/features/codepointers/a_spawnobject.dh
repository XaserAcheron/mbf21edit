/*****************************************************************************
 * mbf21edit - decohack
 * (c) mtrop 2024
 *     MIT License
 *****************************************************************************
 * 
 * Feature: A_SpawnObject
 * 
 * Defines a trio of imp actors; one "tosses" health potions when hurt, the
 * other, drops some armor bonuses in a cross, the third - fires a fireball
 * that fires other fireballs.
 *
 ****************************************************************************/

// Declarations

auto thing A_SpawnObject__Health {}
auto thing A_SpawnObject__Armor {}
auto thing A_SpawnObject__Fireball {}
auto thing A_SpawnObject__FireballImp {}

// Definitions

thing A_SpawnObject__Health : thing MTF_IMP "A_SpawnObject - Health Imp"
{
	//$Category MBF21Edit
	EdNum 3103

	States
	{
		Pain:
			TROO H 2
			TROO H 2 {
				A_Pain()
				A_SpawnObject(MTF_HEALTH_BONUS, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 8.0)
			}
			Goto See
	}
	
}

thing A_SpawnObject__Armor : thing MTF_IMP "A_SpawnObject - Armor Imp"
{
	//$Category MBF21Edit
	EdNum 3104

	States
	{
		Pain:
			TROO H 2
			TROO H 2 {
				A_Pain()
				A_SpawnObject(MTF_ARMOR_BONUS,  16.0,   0.0, 32.0, 0.0, 0.0, 0.0, 0.0)
				A_SpawnObject(MTF_ARMOR_BONUS, -16.0,   0.0, 32.0, 0.0, 0.0, 0.0, 0.0)
				A_SpawnObject(MTF_ARMOR_BONUS,   0.0,   0.0,  8.0, 0.0, 0.0, 0.0, 0.0)
				A_SpawnObject(MTF_ARMOR_BONUS,   0.0,  16.0, 16.0, 0.0, 0.0, 0.0, 0.0)
				A_SpawnObject(MTF_ARMOR_BONUS,   0.0, -16.0, 16.0, 0.0, 0.0, 0.0, 0.0)
			}
			Goto See
	}
	
}

thing A_SpawnObject__Fireball : thing MTF_IMP_FIREBALL "A_SpawnObject - Multi Fireball"
{
	States
	{
		Spawn:
			BAL1 A 4 BRIGHT
			BAL1 B 4 BRIGHT {
				A_SpawnObject(MTF_IMP_FIREBALL, 0.0, 0.0, 0.0, 0.0, 10.0,  10.0,   0.0)
				A_SpawnObject(MTF_IMP_FIREBALL, 0.0, 0.0, 0.0, 0.0, 10.0, -10.0,   0.0)
				A_SpawnObject(MTF_IMP_FIREBALL, 0.0, 0.0, 0.0, 0.0, 10.0,   0.0, -10.0)
				A_SpawnObject(MTF_IMP_FIREBALL, 0.0, 0.0, 0.0, 0.0, 10.0,   0.0,  10.0)
			}
   			Loop
	}
	
}

thing A_SpawnObject__FireballImp : thing MTF_IMP "A_SpawnObject - Multi Fireball Imp"
{
	//$Category MBF21Edit
	EdNum 3105

	States
	{
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 6 A_MonsterProjectile(A_SpawnObject__Fireball)
			Goto See
	}
	
}
